plugins {
    // Provide convenience executables for trying out the examples.
    id 'application'
    id 'nu.studer.jooq' version '3.0.3'
}

dependencies {
    compile project(path: ':networking')

    // database stuff
    implementation "org.jooq:jooq:3.11.12"
    implementation "org.jooq:jooq-meta:3.11.12"
    jooqRuntime "org.jooq:jooq-meta-extensions:3.11.12"
    implementation "org.jooq:jooq-codegen:3.11.12"
    implementation "mysql:mysql-connector-java:5.1.6"
    
    jooqRuntime "mysql:mysql-connector-java:5.1.6"
    
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'com.h2database:h2:1.4.199'
    
    // YAML
    implementation "org.yaml:snakeyaml:1.25"
    
    // web
    implementation 'au.com.codeka:carrot:2.4.2'

    compile 'de.saar.minecraft:shared-resources:1.1.1'
    compile group: 'io.github.classgraph', name: 'classgraph', version: '4.8.60'
}


startScripts.enabled = false

mainClassName = "de.saar.minecraft.broker.Broker"


jooq {
    version = '3.11.12'
    edition = 'OSS'
  
    minecraft(sourceSets.main) {
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            strategy {
                name = 'org.jooq.codegen.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.meta.extensions.ddl.DDLDatabase'

                // make sure that we use the schema MINECRAFT in the generated code
                inputSchema = 'PUBLIC'
                outputSchema = 'MINECRAFT'
                
                properties {
                    property {
                        key = 'scripts'
                        value = 'src/main/resources/database.sql'
                    }
                    property {
                        key = 'sort'
                        value = 'semantic'
                    }

                    // doesn't work yet in jOOQ 3.11
                    /*
                    property {
                        key = 'defaultNameCase'
                        value = 'as_is'
                    }
                    */
                }
                forcedTypes {
                    forcedType {
                        userType = 'de.saar.minecraft.broker.db.GameStatus'
                        enumConverter = true
                        expression = '.*games.status'
                    }
                    forcedType {
                        userType = 'de.saar.minecraft.broker.db.GameLogsDirection'
                        enumConverter = true
                        expression = '.*game_logs.direction'
                    }
                }
            }
            target {
                packageName = 'de.saar.minecraft.broker.db'
             }
        }
    }
}

// Local Variables:
// groovy-indent-offset: 4
// indent-tabs-mode: nil
// End:
