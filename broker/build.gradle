plugins {
    // Provide convenience executables for trying out the examples.
    id 'application'
    id 'nu.studer.jooq' version '3.0.3'
}

dependencies {
    compile project(path: ':networking')

    // database stuff
    implementation "org.jooq:jooq:3.12.3"
    implementation "org.jooq:jooq-meta:3.12.3"
    jooqRuntime "org.jooq:jooq-meta-extensions:3.12.3"
    implementation "org.jooq:jooq-codegen:3.12.3"
    implementation "mysql:mysql-connector-java:5.1.6"
    implementation "org.flywaydb:flyway-core:6.1.4"

    jooqRuntime "mysql:mysql-connector-java:5.1.6"
    
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'com.h2database:h2:1.4.199'
    
    // YAML
    implementation "org.yaml:snakeyaml:1.25"
    
    // web
    implementation 'au.com.codeka:carrot:2.4.2'

    compile 'de.saar.minecraft:shared-resources:1.1.4'
    compile group: 'io.github.classgraph', name: 'classgraph', version: '4.8.60'
}


startScripts.enabled = false

mainClassName = "de.saar.minecraft.broker.Broker"


jooq {
    version = '3.12.3'
    edition = 'OSS'
  
    minecraft(sourceSets.main) {
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            strategy {
                name = 'org.jooq.codegen.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.meta.extensions.ddl.DDLDatabase'
                inputSchema = "MINECRAFT"
                properties {
                    property {
                        key = 'scripts'
                        value = 'src/main/resources/db/migration/*.sql'
                    }
                    property {
                        key = 'sort'
                        value = 'flyway'
                    }
                }
                forcedTypes {
                    forcedType {
                        userType = 'de.saar.minecraft.broker.db.GameStatus'
                        enumConverter = true
                        expression = '.*games.status'
                    }
                    forcedType {
                        userType = 'de.saar.minecraft.broker.db.GameLogsDirection'
                        enumConverter = true
                        expression = '.*game_logs.direction'
                    }
                }
            }
            target {
                packageName = 'de.saar.minecraft.broker.db'
             }
        }
    }
}

// Local Variables:
// groovy-indent-offset: 4
// indent-tabs-mode: nil
// End:
