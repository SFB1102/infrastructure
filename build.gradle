// add guava 27 to the classpath of the *build script*
// see https://github.com/spotbugs/spotbugs-gradle-plugin/issues/120
// solution taken from https://github.com/GoogleContainerTools/jib/issues/591
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("com.google.guava:guava:27.0.1-jre")
    }
}

plugins {
    id 'maven'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'pl.allegro.tech.build.axion-release' version '1.10.1'
    id "com.github.spotbugs" version "2.0.0"
}

allprojects {
    repositories {
        maven { // The google mirror is less flaky than mavenCentral()
            url "https://maven-central.storage-download.googleapis.com/repos/central/data/" }
        mavenLocal()
    }
    
    scmVersion {
        tag {
            prefix = ''
        }
    }
    version = scmVersion.version
}

subprojects {
    apply plugin: "maven"
    apply plugin: "java"
    apply plugin: 'com.github.johnrengelman.shadow'
    
    sourceCompatibility = 1.11
    
    spotbugs {
        toolVersion = '4.0.0-beta3'
        ignoreFailures = true
    }
    tasks.withType(com.github.spotbugs.SpotBugsTask) {
        reports {
            xml.enabled = false
            text.enabled = true
            html.enabled = false
        }
    }
    
    dependencies {
        compileOnly "javax.annotation:javax.annotation-api:1.2"
        testImplementation "junit:junit:4.12"
        testImplementation "org.mockito:mockito-core:2.25.1"
    }
}

// Local Variables:
// groovy-indent-offset: 4
// indent-tabs-mode: nil
// End:
