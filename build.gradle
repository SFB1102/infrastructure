// add guava 27 to the classpath of the *build script*
// see https://github.com/spotbugs/spotbugs-gradle-plugin/issues/120
// solution taken from https://github.com/GoogleContainerTools/jib/issues/591
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("com.google.guava:guava:27.0.1-jre")
    }
}

plugins {
    id 'maven-publish'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'pl.allegro.tech.build.axion-release' version '1.10.1'
    id "com.github.spotbugs" version "2.0.0"
}

allprojects {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/minecraft-saar/simple-architect")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
            }
        }
        maven { // The google mirror is less flaky than mavenCentral()
            url "https://maven-central.storage-download.googleapis.com/repos/central/data/" }
        // maven central has packages that are missing from the google mirror
        mavenCentral()
        mavenLocal()
    }
    scmVersion {
        tag {
            prefix = 'release'
        }
    }
    version = scmVersion.version
}

subprojects {
    apply plugin: "maven-publish"
    apply plugin: "java"
    apply plugin: 'com.github.johnrengelman.shadow'
    
    sourceCompatibility = 1.11
    group 'de.saar.minecraft.infrastructure'

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    
    javadoc {
	    failOnError = false
    }
    
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/minecraft-saar/infrastructure")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                    password = project.findProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
                }
            }
        }
        publications {
            "$project.name"(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }

        }
    }
    
    spotbugs {
        toolVersion = '4.0.0-beta3'
        ignoreFailures = true
    }
    tasks.withType(com.github.spotbugs.SpotBugsTask) {
        reports {
            xml.enabled = false
            text.enabled = true
            html.enabled = false
        }
    }
    
    dependencies {
        compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.8.2'
        compileOnly "javax.annotation:javax.annotation-api:1.2"
        testImplementation "junit:junit:4.12"
        testImplementation "org.mockito:mockito-core:2.25.1"
    }
}

// Local Variables:
// groovy-indent-offset: 4
// indent-tabs-mode: nil
// End:
